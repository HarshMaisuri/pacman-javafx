plugins {
    id 'java'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group 'de.amr.games.pacman'
version '1.0'

ext.os = org.gradle.internal.os.OperatingSystem.current()
println "*** Building on ${os.familyName} / ${os.name} / ${os.version} / ${System.getProperty("os.arch")}."

application {
    mainModule = 'de.amr.games.pacman.ui.fx.v3d'
    mainClass  = 'de.amr.games.pacman.ui.fx.v3d.Main'
}

dependencies {
    implementation project(':pacman-core')
    implementation project(':pacman-ui-fx-2d')
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

javafx {
    version = '21.0.2'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'pacman-javafx-3d'
    }
    jpackage {
        // not sure where these are finally appearing
        installerOptions = [
            '--description', 'JavaFX 3D',
            '--copyright', '(c) 2021-2024 Armin Reichert',
        ]
        if (os.isWindows()) {
            icon = 'pacman.ico'
            installerType = 'exe'
            installerOptions.addAll(['--win-dir-chooser', '--win-menu', '--win-shortcut'])
        } else if (os.isLinux()) {
            // doesn't create working installer
            installerType = 'deb'
        }
    }
}

// needed?
jlinkZip {
    group = 'distribution'
}