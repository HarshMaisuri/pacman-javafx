plugins {
    id 'java'
    //id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1' //  adds also 'application' plugin
}

group 'de.amr.games.pacman'
version '1.0'

ext.os = org.gradle.internal.os.OperatingSystem.current()

application {
    mainModule = 'de.amr.games.pacman.ui.fx.v3d'
    mainClass  = 'de.amr.games.pacman.ui.fx.v3d.Main'
}

dependencies {
    implementation project(':pacman-core')
    implementation project(':pacman-ui-fx-2d')
    implementation 'org.tinylog:tinylog-api:2.7.0'
    implementation 'org.tinylog:tinylog-impl:2.7.0'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javafx {
    version = '21.0.2'
    modules = ['javafx.controls', 'javafx.media']
}

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'pacman-javafx-3d'
    }
    jpackage {
        if (os.isWindows()) {
            installerType = 'exe'
            installerOptions = ['--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (os.isLinux()) {
            installerType = 'deb'
        } else if (os.isMacOsX()) {
            installerType = 'dmg'
        }
        vendor = 'Armin Reichert'
        icon = 'pacman.ico'
    }
}

jlinkZip {
    group = 'distribution'
}
